plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.32.0' apply false
    id 'groovy'
}
apply plugin: 'com.jfrog.artifactory'

group "gov.cms.ab2d.plugin"
version "1.0.2"

sourceCompatibility = 17
targetCompatibility = 17

ext {
    artifactory_contextUrl = project.hasProperty('artifactory_contextUrl') ? project.artifactory_contextUrl
            : System.getenv().get("ARTIFACTORY_URL")
    artifactory_user = project.hasProperty('artifactory_user') ? project.artifactory_user
            : System.getenv().get('ARTIFACTORY_USER')
    artifactory_password = project.hasProperty('artifactory_password') ? project.artifactory_password
            : System.getenv().get('ARTIFACTORY_PASSWORD')

    sourcesRepo = 'ab2d-maven-repo'
    deployerRepo = 'ab2d-main'
    resolverRepo = 'ab2d-main'
}

gradlePlugin {
    // Define the plugin
    plugins {
        parentPlug {
            id = 'gov.cms.ab2d.plugin'
            implementationClass = 'gov.cms.ab2d.plugin.ParentPlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = "${artifactory_contextUrl}/${deployerRepo}"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
}

artifactory {
    contextUrl = project.artifactory_contextUrl

    publish {
        repository {
            repoKey = "${deployerRepo}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = false
        }
    }
    resolve {
        repository {
            repoKey = "${resolverRepo}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
